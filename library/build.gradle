apply plugin: 'com.android.library'

def versionMajor = 4
def versionMinor = 1
def versionPatch = 0

dependencies {
    implementation "com.android.support:support-media-compat:27.0.0"
    implementation "com.android.support:appcompat-v7:27.0.0"

    // ExoPlayer
    api "com.google.android.exoplayer:exoplayer-core:r2.5.4"
    api "com.google.android.exoplayer:exoplayer-dash:r2.5.4"
    api "com.google.android.exoplayer:exoplayer-hls:r2.5.4"
    api "com.google.android.exoplayer:exoplayer-smoothstreaming:r2.5.4"
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

// ======== MAVEN DEPLOY to local cache or nexus repo (to make accessible to local projects) (command: ./gradlew clean aRel uploadArchives) ========
uploadArchives {
    repositories {
        // deploy to local
        mavenDeployer {
            repository (url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = libraryGroupId
            pom.artifactId = libraryBaseName
            pom.version = libraryVersion //Normally I would append "-SNAPSHOT" but newer gradle/maven can't always find them
            pom.packaging = 'aar'
            uniqueVersion = false // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// ======= BINTRAY DEPLOY (command: $ ./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=false) ====
publish {
    userOrg = ""
    groupId = libraryGroupId
    artifactId = libraryBaseName
    uploadName = 'ExoMedia'
    publishVersion = libraryVersion
    desc = ""
    autoPublish = false
    dryRun = false
}
